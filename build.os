#Использовать 1commands
#Использовать fs

Процедура Собрать()
	СобратьБиблиотекуDotnet();
	СобратьБиблиотекуOscript();
КонецПроцедуры

Процедура СобратьБиблиотекуDotnet()
	ВыполнитьКоманду("dotnet build oscript-compressor -c Release");
КонецПроцедуры

Процедура СобратьБиблиотекуOscript()
	ПодготовитьКаталогComponents();
	ВыполнитьКоманду("opm build");
КонецПроцедуры

Процедура ПодготовитьКаталогComponents()

	СоответствиеПапок = Новый Соответствие();
	СоответствиеПапок.Вставить("net4", "net48");
	СоответствиеПапок.Вставить("dotnet", "net6.0");

	ПомещаемыеРесурсы = Новый Массив();
	ПомещаемыеРесурсы.Добавить("1script_compressor.dll");
	
	ПомещаемыеРесурсы.Добавить("EasyCompressor.dll");
	ПомещаемыеРесурсы.Добавить("EasyCompressor.Snappier.dll");
	ПомещаемыеРесурсы.Добавить("EasyCompressor.LZ4.dll");
	ПомещаемыеРесурсы.Добавить("EasyCompressor.ZstdSharp.dll");
	
	ПомещаемыеРесурсы.Добавить("BrotliSharpLib.dll");
	ПомещаемыеРесурсы.Добавить("Snappier.dll");
	ПомещаемыеРесурсы.Добавить("ZstdSharp.dll");
	ПомещаемыеРесурсы.Добавить("K4os.Compression.LZ4.dll");
	ПомещаемыеРесурсы.Добавить("K4os.Compression.LZ4.Streams.dll");
	ПомещаемыеРесурсы.Добавить("K4os.Hash.xxHash.dll");

	ПомещаемыеРесурсы.Добавить("System.Memory.dll");
	ПомещаемыеРесурсы.Добавить("System.Buffers.dll");
	ПомещаемыеРесурсы.Добавить("System.Runtime.CompilerServices.Unsafe.dll");
	ПомещаемыеРесурсы.Добавить("System.Threading.Tasks.Extensions.dll");
	
	// Копируем скомпилированные библиотеки в папку Components
	Для Каждого Соответствие Из СоответствиеПапок Цикл
		ИмяКаталогаOscript = Соответствие.Ключ;
		ИмяКаталогаDotnet = Соответствие.Значение;

		ПутьККаталгуOscript = ОбъединитьПути(ТекущийКаталог(), "Components", ИмяКаталогаOscript);
		ПутьККаталгуDotnet = ОбъединитьПути(ТекущийКаталог(), "oscript-compressor/bin/Release", ИмяКаталогаDotnet);

		ФС.ОбеспечитьПустойКаталог(ПутьККаталгуOscript);

		Для Каждого ИмяРесурса Из ПомещаемыеРесурсы Цикл
			ПутьИсточник = ОбъединитьПути(ПутьККаталгуDotnet, ИмяРесурса);
			ПутьПриемник = ОбъединитьПути(ПутьККаталгуOscript, ИмяРесурса);

			Если ФС.ФайлСуществует(ПутьИсточник) Тогда
				ПереместитьФайл(ПутьИсточник, ПутьПриемник);
			ИначеЕсли ФС.КаталогСуществует(ПутьИсточник) Тогда
				ФС.КопироватьСодержимоеКаталога(ПутьИсточник, ПутьПриемник);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьКоманду(Команда)
	КомандныйФайл = Новый КомандныйФайл;
	КомандныйФайл.ПоказыватьВыводНемедленно(Истина);
	КомандныйФайл.УстановитьКодировкуВывода(КодировкаТекста.UTF8);
	КомандныйФайл.Создать();
	КомандныйФайл.ДобавитьКоманду(Команда);
	КодВозврата = КомандныйФайл.Исполнить();
	Если Не КодВозврата = 0 Тогда
		ВызватьИсключение "Не удалось выполнить сборку";
	КонецЕсли;
КонецПроцедуры

Собрать();