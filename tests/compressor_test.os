// BSLLS:DuplicatedInsertionIntoCollection-off
// BSLLS:CognitiveComplexity-off
// BSLLS:LineLength-off
// BSLLS:DuplicateStringLiteral-off

#Использовать asserts
#Использовать tempfiles
#Использовать ".."

Перем МенеджерВременныхФайлов; // МенеджерВременныхФайлов
Перем Потоки; // Массив

Процедура ПередЗапускомТестов() Экспорт

	МенеджерВременныхФайлов = Новый МенеджерВременныхФайлов();
	Потоки = Новый Массив();

КонецПроцедуры

&После
Процедура ПослеЗапускаТеста() Экспорт

	ЗакрытьПотоки();
	МенеджерВременныхФайлов.Удалить();

КонецПроцедуры

&Тест
Процедура ТестДолжен_УпаковковатьРаспаковковатьДвоичныеДанные() Экспорт

	Компрессоры = Компрессоры();
	ИсходныеДанные = ТестовыеДвоичныеДанные();

	Для Каждого Компрессор Из Компрессоры Цикл
		УпакованныеДанные = Компрессор.Упаковать(ИсходныеДанные);
		РаспакованныеДанные = Компрессор.Распаковать(УпакованныеДанные);
		
		Ожидаем.Что(РаспакованныеДанные, Компрессор).Равно(ИсходныеДанные);
	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_УпаковатьДвоичныеДанныеВПотокВПамяти_РаспаковатьДвоичныеДанныеВПотокВПамяти() Экспорт

	Компрессоры = Компрессоры();
	ИсходныеДанные = ТестовыеДвоичныеДанные();

	Для Каждого Компрессор Из Компрессоры Цикл

		Поток = Новый ПотокВПамяти();
		Компрессор.Упаковать(ИсходныеДанные, Поток);
		УпакованныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();

		Поток = Новый ПотокВПамяти();
		Компрессор.Распаковать(УпакованныеДанные, Поток);
		РаспакованныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные();

		Ожидаем.Что(РаспакованныеДанные, Компрессор).Равно(ИсходныеДанные);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_УпаковковатьДвоичныеДанные_РаспаковатьПотокВПамятиВДвоичныеДанные() Экспорт

	Компрессоры = Компрессоры();
	ИсходныеДанные = ТестовыеДвоичныеДанные();

	Для Каждого Компрессор Из Компрессоры Цикл

		УпакованныеДанные = Компрессор.Упаковать(ИсходныеДанные);

		Буфер = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(УпакованныеДанные);
		Поток = Новый ПотокВПамяти(Буфер);
		РаспакованныеДанные = Компрессор.Распаковать(Поток);
		
		Ожидаем.Что(РаспакованныеДанные, Компрессор).Равно(ИсходныеДанные);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_УпаковатьПотокВПамятиВДвоичныеДанные_РаспаковатьПотокВПамятиВДвоичныеДанные() Экспорт

	Компрессоры = Компрессоры();
	ИсходныеДанные = ТестовыеДвоичныеДанные();
		
	Для Каждого Компрессор Из Компрессоры Цикл

		Буфер = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ИсходныеДанные);
		Поток = Новый ПотокВПамяти(Буфер);
		УпакованныеДанные = Компрессор.Упаковать(Поток);

		Буфер = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(УпакованныеДанные);
		Поток = Новый ПотокВПамяти(Буфер);
		РаспакованныеДанные = Компрессор.Распаковать(Поток);

		Ожидаем.Что(РаспакованныеДанные, Компрессор).Равно(ИсходныеДанные);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_УпаковатьПотокВПоток_РаспаковатьПотокВПоток() Экспорт

	Компрессоры = Компрессоры();
	ИсходныеДанные = ТестовыеДвоичныеДанные();
	
	Для Каждого Компрессор Из Компрессоры Цикл

		Буфер = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ИсходныеДанные);
		ВходящийПоток = Новый ПотокВПамяти(Буфер);
		ИсходящийПоток = Новый ПотокВПамяти();
		Компрессор.Упаковать(ВходящийПоток, ИсходящийПоток);

		Буфер = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ИсходящийПоток.ЗакрытьИПолучитьДвоичныеДанные());
		ВходящийПоток = Новый ПотокВПамяти(Буфер);
		ИсходящийПоток = Новый ПотокВПамяти();
		Компрессор.Распаковать(ВходящийПоток, ИсходящийПоток);
		РаспакованныеДанные = ИсходящийПоток.ЗакрытьИПолучитьДвоичныеДанные();

		Ожидаем.Что(РаспакованныеДанные, Компрессор).Равно(ИсходныеДанные);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_УпаковатьФайловыйПотокВДвоичныеДанные_РаспаковатьФайловыйПотокВДвоичныеДанные() Экспорт

	Компрессоры = Компрессоры();

	ПутьКФайлу = МенеджерВременныхФайлов.НовоеИмяФайла();
	ИсходныеДанные = ТестовыеДвоичныеДанные();
	ИсходныеДанные.Записать(ПутьКФайлу);

	ИсходныйФайловыйПоток = Новый ФайловыйПоток(ПутьКФайлу, РежимОткрытияФайла.Открыть);
	Потоки.Добавить(ИсходныйФайловыйПоток);

	Для Каждого Компрессор Из Компрессоры Цикл

		ИсходныйФайловыйПоток.Перейти(0, ПозицияВПотоке.Начало);
		ПутьКУпакованномуФайлу = МенеджерВременныхФайлов.НовоеИмяФайла();

		УпакованныеДанные = Компрессор.Упаковать(ИсходныйФайловыйПоток);
		УпакованныеДанные.Записать(ПутьКУпакованномуФайлу);

		УпакованныйПоток = Новый ФайловыйПоток(ПутьКУпакованномуФайлу, РежимОткрытияФайла.Открыть);
		Потоки.Добавить(УпакованныйПоток);

		РаспакованныеДанные = Компрессор.Распаковать(УпакованныйПоток);

		Ожидаем.Что(РаспакованныеДанные, Компрессор).Равно(ИсходныеДанные);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_УпаковатьИРаспаковатьСУказаниемУровняСжатия() Экспорт

	ИсходныеДанные = ТестовыеДвоичныеДанные();

	Компрессоры = Новый Массив();
	Компрессоры.Добавить(Новый GZipКомпрессор(УровеньСжатияДанных.Оптимальный));
	Компрессоры.Добавить(Новый GZipКомпрессор(УровеньСжатияДанных.Быстрый));
	Компрессоры.Добавить(Новый GZipКомпрессор(УровеньСжатияДанных.БезСжатия));

	Компрессоры.Добавить(Новый DeflateКомпрессор(УровеньСжатияДанных.Оптимальный));
	Компрессоры.Добавить(Новый DeflateКомпрессор(УровеньСжатияДанных.Быстрый));
	Компрессоры.Добавить(Новый DeflateКомпрессор(УровеньСжатияДанных.БезСжатия));

	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L00_FAST));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L03_HC));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L04_HC));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L05_HC));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L06_HC));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L07_HC));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L08_HC));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L09_HC));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L10_OPT));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L11_OPT));
	Компрессоры.Добавить(Новый LZ4Компрессор(УровеньСжатияДанныхLZ4.L12_MAX));

	Компрессоры.Добавить(Новый ZstdКомпрессор(УровеньСжатияДанныхZstd.БезСжатия));
	Компрессоры.Добавить(Новый ZstdКомпрессор(УровеньСжатияДанныхZstd.Быстрый));
	Компрессоры.Добавить(Новый ZstdКомпрессор(УровеньСжатияДанныхZstd.ПоУмолчанию));
	Компрессоры.Добавить(Новый ZstdКомпрессор(УровеньСжатияДанныхZstd.Оптимальный));
	Компрессоры.Добавить(Новый ZstdКомпрессор(УровеньСжатияДанныхZstd.НаименьшийРазмер));

	Компрессоры.Добавить(Новый ZstdКомпрессор(-131072));
	Компрессоры.Добавить(Новый ZstdКомпрессор(-22));
	Компрессоры.Добавить(Новый ZstdКомпрессор(-1));
	Компрессоры.Добавить(Новый ZstdКомпрессор(3));
	Компрессоры.Добавить(Новый ZstdКомпрессор(2));

	Компрессоры.Добавить(Новый ZLibКомпрессор(УровеньСжатияДанных.Оптимальный));
	Компрессоры.Добавить(Новый ZLibКомпрессор(УровеньСжатияДанных.Быстрый));
	Компрессоры.Добавить(Новый ZLibКомпрессор(УровеньСжатияДанных.БезСжатия));

	Для Каждого Компрессор Из Компрессоры Цикл
		УпакованныеДанные = Компрессор.Упаковать(ИсходныеДанные);
		РаспакованныеДанные = Компрессор.Распаковать(УпакованныеДанные);

		Ожидаем.Что(РаспакованныеДанные, Компрессор).Равно(ИсходныеДанные);
	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьОшибкиСНевернымиАргументами() Экспорт

	ДвоичныеДанные = ТестовыеДвоичныеДанные();
	ОшибочноеЗначение = Новый Массив();

	ТестовыеСлучаи = Новый ТаблицаЗначений();
	ТестовыеСлучаи.Колонки.Добавить("Метод");
	ТестовыеСлучаи.Колонки.Добавить("Данные");
	ТестовыеСлучаи.Колонки.Добавить("Поток");
	ТестовыеСлучаи.Колонки.Добавить("Сообщение");

	ТестовыйСлучай = ТестовыеСлучаи.Добавить();
	ТестовыйСлучай.Метод = "Упаковать";
	ТестовыйСлучай.Данные = ОшибочноеЗначение;
	ТестовыйСлучай.Сообщение = "Неверный тип аргумента 'data'";

	ТестовыйСлучай = ТестовыеСлучаи.Добавить();
	ТестовыйСлучай.Метод = "Упаковать";
	ТестовыйСлучай.Данные = ОшибочноеЗначение;
	ТестовыйСлучай.Поток = ОшибочноеЗначение;
	ТестовыйСлучай.Сообщение = "Неверный тип аргумента 'data'";

	ТестовыйСлучай = ТестовыеСлучаи.Добавить();
	ТестовыйСлучай.Метод = "Упаковать";
	ТестовыйСлучай.Данные = ДвоичныеДанные;
	ТестовыйСлучай.Поток = ОшибочноеЗначение;
	ТестовыйСлучай.Сообщение = "Неверный тип аргумента 'outputStream'";

	ТестовыйСлучай = ТестовыеСлучаи.Добавить();
	ТестовыйСлучай.Метод = "Распаковать";
	ТестовыйСлучай.Данные = ОшибочноеЗначение;
	ТестовыйСлучай.Сообщение = "Неверный тип аргумента 'data'";

	ТестовыйСлучай = ТестовыеСлучаи.Добавить();
	ТестовыйСлучай.Метод = "Распаковать";
	ТестовыйСлучай.Данные = ОшибочноеЗначение;
	ТестовыйСлучай.Поток = ОшибочноеЗначение;
	ТестовыйСлучай.Сообщение = "Неверный тип аргумента 'data'";

	ТестовыйСлучай = ТестовыеСлучаи.Добавить();
	ТестовыйСлучай.Метод = "Распаковать";
	ТестовыйСлучай.Данные = ДвоичныеДанные;
	ТестовыйСлучай.Поток = ОшибочноеЗначение;
	ТестовыйСлучай.Сообщение = "Неверный тип аргумента 'outputStream'";

	Компрессор = Новый GZipКомпрессор();

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(ТестовыйСлучай.Данные);
		ПараметрыМетода.Добавить(ТестовыйСлучай.Поток);

		Ожидаем.Что(Компрессор)
			.Метод(ТестовыйСлучай.Метод, ПараметрыМетода)
			.ВыбрасываетИсключение(ТестовыйСлучай.Сообщение);

	КонецЦикла;
	
КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьСостояниеПотоковПриУпаковке() Экспорт

	Компрессоры = Компрессоры();
	ТестовыеДанные = ТестовыеДвоичныеДанные();

	ИменаМетодов = Новый Массив();
	ИменаМетодов.Добавить("Упаковать");
	ИменаМетодов.Добавить("Распаковать");

	КомбинацииДанных = Новый Соответствие();
	КомбинацииДанных.Вставить("Поток", "Поток");
	КомбинацииДанных.Вставить("ДД", "Поток");
	КомбинацииДанных.Вставить("Поток", "ДД");

	Для Каждого Компрессор Из Компрессоры Цикл
	
		Для Каждого Метод Из ИменаМетодов Цикл

			Для Каждого Комбинация Из КомбинацииДанных Цикл

				Если Метод = "Упаковать" Тогда
					ИсходныеДанные = ТестовыеДанные;
				Иначе
					ИсходныеДанные = Компрессор.Упаковать(ТестовыеДанные);
				КонецЕсли;

				Если Комбинация.Ключ = "Поток" Тогда
					Буфер = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ИсходныеДанные);
					ВходящиеДанные = Новый ПотокВПамяти(Буфер);
				Иначе
					ВходящиеДанные = ИсходныеДанные;
				КонецЕсли;

				Если Комбинация.Значение = "Поток" Тогда
					ИсходящиеДанные = Новый ПотокВПамяти();
				Иначе
					ИсходящиеДанные = Неопределено;
				КонецЕсли;

				Если Метод = "Упаковать" Тогда
					Компрессор.Упаковать(ВходящиеДанные, ИсходящиеДанные);
				Иначе
					Компрессор.Распаковать(ВходящиеДанные, ИсходящиеДанные);
				КонецЕсли;

				Сообщение = СтрШаблон("%1.%2(%3, %4)", Компрессор, Метод, Комбинация.Ключ, Комбинация.Значение);

				ПроверитьСостояниеПотока(ВходящиеДанные, Сообщение);
				ПроверитьСостояниеПотока(ИсходящиеДанные, Сообщение);

			КонецЦикла;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ПроверитьСостояниеПотока(Поток, Сообщение)

	Если ТипЗнч(Поток) <> Тип("ПотокВПамяти") Тогда
		Возврат;
	КонецЕсли;

	Ожидаем.Что(Поток.ДоступноЧтение, "Доступно чтение потока. " + Сообщение).ЭтоИстина();
	Ожидаем.Что(Поток.ДоступнаЗапись, "Доступна запись в поток. " + Сообщение).ЭтоИстина();
	Ожидаем.Что(Поток.ТекущаяПозиция(), "Текущая позиция потока. " + Сообщение).Равно(Поток.Размер());

	Поток.Перейти(0, ПозицияВПотоке.Начало);

	Буфер = Новый БуферДвоичныхДанных(1);
	Поток.Записать(Буфер, 0, Буфер.Размер);

КонецПроцедуры

Процедура ЗакрытьПотоки()
	Количество = Потоки.Количество();
	Для Номер = 1 По Количество Цикл
		Поток = Потоки[Количество - Номер];
		Попытка
			Поток.Закрыть();
		Исключение
			Потоки.Удалить(Поток);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

Функция Компрессоры()
	Массив = Новый Массив();
	Массив.Добавить(Новый GZipКомпрессор());
	Массив.Добавить(Новый DeflateКомпрессор());
	Массив.Добавить(Новый BrotliКомпрессор());
	Массив.Добавить(Новый LZ4Компрессор());
	Массив.Добавить(Новый SnappyКомпрессор());
	Массив.Добавить(Новый ZstdКомпрессор());
	Массив.Добавить(Новый ZLibКомпрессор());
	
	Возврат Массив;
КонецФункции

Функция ТестовыеДвоичныеДанные()
	Возврат ПолучитьДвоичныеДанныеИзСтроки("https://github.com/Stivo182/oscript-compressor");
КонецФункции