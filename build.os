#Использовать 1commands
#Использовать fs

Процедура Собрать()
	СобратьБиблиотекуDotnet();
	СобратьБиблиотекуOscript();
КонецПроцедуры

Процедура СобратьБиблиотекуDotnet()
	КомандныйФайл = Новый КомандныйФайл;
	КомандныйФайл.ПоказыватьВыводНемедленно(Истина);
	КомандныйФайл.УстановитьКодировкуВывода(КодировкаТекста.UTF8);
	КомандныйФайл.Создать();
	КомандныйФайл.ДобавитьКоманду("dotnet build oscript-compressor -c Release");
	КомандныйФайл.Исполнить();
КонецПроцедуры

Процедура СобратьБиблиотекуOscript()
	ПодготовитьКаталогComponents();

	КомандныйФайл = Новый КомандныйФайл;
	КомандныйФайл.ПоказыватьВыводНемедленно(Истина);
	КомандныйФайл.Создать();
	КомандныйФайл.ДобавитьКоманду("opm build");
	КомандныйФайл.Исполнить();	
КонецПроцедуры

Процедура ПодготовитьКаталогComponents()

	СоответствиеПапок = Новый Соответствие();
	СоответствиеПапок.Вставить("net4", "net48");
	СоответствиеПапок.Вставить("dotnet", "net6.0");

	ПомещаемыеБиблиотеки = Новый Массив();
	ПомещаемыеБиблиотеки.Добавить("OscriptCompressor.dll");
	ПомещаемыеБиблиотеки.Добавить("EasyCompressor.dll");
	ПомещаемыеБиблиотеки.Добавить("EasyCompressor.BrotliNET.dll");
	ПомещаемыеБиблиотеки.Добавить("Brotli.Core.dll");
	
	// Копируем скомпилированные библиотеки в папку Components
	Для Каждого Соответствие Из СоответствиеПапок Цикл
		ИмяКаталогаOscript = Соответствие.Ключ;
		ИмяКаталогаDotnet = Соответствие.Значение;

		ПутьККаталгуOscript = ОбъединитьПути(ТекущийКаталог(), "Components", ИмяКаталогаOscript);
		ПутьККаталгуDotnet = ОбъединитьПути(ТекущийКаталог(), "oscript-compressor/bin/Release", ИмяКаталогаDotnet);

		ФС.ОбеспечитьПустойКаталог(ПутьККаталгуOscript);

		Для Каждого ИмяФайла Из ПомещаемыеБиблиотеки Цикл
			ИмяФайлаИсточник = ОбъединитьПути(ПутьККаталгуDotnet, ИмяФайла);
			ИмяФайлаПриемник = ОбъединитьПути(ПутьККаталгуOscript, ИмяФайла);
			Если ФС.ФайлСуществует(ИмяФайлаИсточник) Тогда
				ПереместитьФайл(ИмяФайлаИсточник, ИмяФайлаПриемник);
			КонецЕсли;
		КонецЦикла;

		ПереместитьФайл(
			ОбъединитьПути(ПутьККаталгуOscript, "OscriptCompressor.dll"),
			ОбъединитьПути(ПутьККаталгуOscript, "1script_Compressor.dll"));
	КонецЦикла;

	// Копируем нативные библиотеки в папку Components/net4
	ФС.КопироватьСодержимоеКаталога(
		ОбъединитьПути(ТекущийКаталог(), "lib", "net4"),
		ОбъединитьПути(ТекущийКаталог(), "Components", "net4"));

КонецПроцедуры

Собрать();